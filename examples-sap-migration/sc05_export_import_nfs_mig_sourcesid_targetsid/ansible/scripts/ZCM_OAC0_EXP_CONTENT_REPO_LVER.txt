*---------------------------------------------------------------------*
* Program Name        :  ZCM_OAC0_EXP_CONTENT_REPO_LVER               *
* Title               :  Target DB versions                           *
* Purpose             :  To migrate the content repository - OAC0     *
*---------------------------------------------------------------------*
* Related Transaction OR Standard Program References                  *
* OAC0                                                                *
*---------------------------------------------------------------------*
*	Tables              :   SELECT    UPDATE      INSERT      DELETE    *
* XXXXX                     X                                         *
*---------------------------------------------------------------------*
* Program Change History                                              *
*---------------------------------------------------------------------*
*Date        Version   Author       TR#/Local     Description         *
*---------------------------------------------------------------------*
*09-14-2020  Initial Fathima Hussain  Local   To fetch content reposit*
*                                             ory - OAC0              *
*05-25-2021  V001    Fathima Hussain  Local   Column headings changed *
*10-14-2022  V002    Soumya Ray       Local   To pass 'No data found' *
*                                             in case of no data in sy*
*                                             stem                    *
*02-02-2023  V003    Soumya Ray      Local    Removing inline declarat*
*                                             ion for lower version   *
*---------------------------------------------------------------------*

REPORT zcm_oac0_exp_content_repo_lver.

TYPES: BEGIN OF s_toaar.
      INCLUDE STRUCTURE toaar.

TYPES: objecttext TYPE toasr-objecttext,
       language   TYPE toasr-language,
       END OF s_toaar,

       t_toaar TYPE STANDARD TABLE OF s_toaar WITH DEFAULT KEY.

DATA : BEGIN OF i_srep OCCURS 1.
       INCLUDE STRUCTURE scms_screp.

DATA:  mark       TYPE sy-input,
       text(80),
       crea_t     TYPE tims,
       crea_d     TYPE d,
       chng_t     TYPE tims,
       chng_d     TYPE d,
       crea_u(80),
       chng_u(80),
     END OF i_srep.

FORM get_config TABLES lt_input  STRUCTURE tab512
                       lt_output STRUCTURE tab512.

  CONSTANTS: lc_sep        TYPE c VALUE '|'               LENGTH 1,
             lc_export     TYPE c VALUE 'EXPORT'          LENGTH 20,
             lc_title      TYPE c VALUE
                                  'ContentRepository'     LENGTH 20,
* Begin of changes in V002
             lc_null       TYPE c VALUE 'NULL'            LENGTH 4,
             lc_noinput    TYPE c VALUE 'No input passed' LENGTH 20,
             lc_input      TYPE c VALUE 'Incorrect input' LENGTH 15,
             lc_msg_nodata TYPE c VALUE 'No data found'   LENGTH 20.
* End of changes in V002

  DATA: ls_input           TYPE tab512,
        ls_output          TYPE tab512,
        lv_action          TYPE char8,
        creptypes_all      TYPE vrm_values,
        creptype           TYPE vrm_value,
        lv_test_connection TYPE string,
        itab               LIKE dd07v OCCURS 1 WITH HEADER LINE,
        crepversion        TYPE vrm_value,
        crepversions       TYPE vrm_values,
        crepstype          TYPE vrm_value,
        crepstypes         TYPE vrm_values,
        pvalue             TYPE tpfyvalue-value,
        path               TYPE string,
        len                TYPE i,
        del(1),
        last(1),
        platform           TYPE i,
        tst                TYPE timestamp,
        user_name          TYPE usr01-bname,
        user_address       TYPE addr3_val,
        i_toaar_initial    TYPE t_toaar WITH HEADER LINE,
        i_srep_initial     LIKE i_srep OCCURS 1 WITH HEADER LINE,
        i_sdokdocspt       LIKE sdokdocspt OCCURS 1 WITH HEADER LINE,
        lv_document        TYPE sdok_descr. "+V003

* Begin of changes V002
  " Title
  ls_output = lc_title.
  APPEND ls_output TO lt_output.
  CLEAR: ls_output.
* End of changes V002

  " Read the action required from Python interface
  CLEAR: ls_input, lv_action.
  READ TABLE lt_input INTO ls_input INDEX 1.
* Begin of changes in V002
  IF sy-subrc EQ 0.
    lv_action = ls_input.
  ELSE.
    ls_output = lc_noinput.
    APPEND ls_output TO lt_output.
    CLEAR ls_output.
    RETURN.
  ENDIF.
* End of changes in V002

  CASE lv_action.

    WHEN lc_export.

***  Storage Types   ***
      REFRESH:  creptypes_all.
      CALL FUNCTION 'DDUT_DOMVALUES_GET'
        EXPORTING
          name          = 'SCMS_CRTYP'
          langu         = sy-langu
        TABLES
          dd07v_tab     = itab
        EXCEPTIONS
          illegal_input = 1
          OTHERS        = 2.
      CHECK sy-subrc = 0.

      SORT itab BY ddtext.
      LOOP AT itab.
        creptype-key  = itab-domvalue_l.
        creptype-text = itab-ddtext.
        APPEND creptype TO creptypes_all.
      ENDLOOP.

***  Version description  ***
      REFRESH: itab.
      CALL FUNCTION 'DDUT_DOMVALUES_GET'
        EXPORTING
          name          = 'SCMS_CRVDB' "name
          langu         = sy-langu
        TABLES
          dd07v_tab     = itab
        EXCEPTIONS
          illegal_input = 1
          OTHERS        = 2.
      IF sy-subrc <> 0.

      ENDIF.

      REFRESH crepversions.
      SORT itab BY ddtext.
      LOOP AT itab.
        crepversion-key  = itab-domvalue_l.
        crepversion-text = itab-ddtext.
        APPEND crepversion TO crepversions.
      ENDLOOP.

***   Repository Sub-type  ***
      REFRESH : itab.
      CALL FUNCTION 'DDUT_DOMVALUES_GET'
        EXPORTING
          name          = 'SCMS_CRSTP' "name
          langu         = sy-langu
        TABLES
          dd07v_tab     = itab
        EXCEPTIONS
          illegal_input = 1
          OTHERS        = 2.
      IF sy-subrc <> 0.

      ENDIF.

      REFRESH crepstypes.
      SORT itab BY ddtext.
      LOOP AT itab.
        crepstype-key  = itab-domvalue_l.
        crepstype-text = itab-ddtext.
        APPEND crepstype TO crepstypes.
      ENDLOOP.

*** Storage Path   ***

* Perform file_get_name in program SAPLSCMS_AO_API using l_file file.
      CALL FUNCTION 'SXPG_PROFILE_PARAMETER_GET'
        EXPORTING
          parameter_name  = 'DIR_GLOBAL'
        IMPORTING
          parameter_value = pvalue.

      " Form check_and_add_delimiter
      path = pvalue.

      IF path CA '/\'.
        del = path+sy-fdpos(1).
      ELSE.
        IF sy-batch <> 'X'.
          CALL METHOD cl_gui_frontend_services=>get_platform
            RECEIVING
              platform             = platform
            EXCEPTIONS
              error_no_gui         = 1
              cntl_error           = 2
              not_supported_by_gui = 3
              OTHERS               = 4.
          IF sy-subrc <> 0.
            RAISE cntl_error.
          ENDIF.

* Changing the path seperator based on the OS
          IF platform = cl_gui_frontend_services=>platform_linux OR
            platform = cl_gui_frontend_services=>platform_macosx.
            del = '/'.
          ELSEIF platform =
            cl_gui_frontend_services=>platform_windowsxp.
            del = '\'.
          ENDIF.
        ENDIF.
      ENDIF.
      len = strlen( path ).
      len = len - 1.
      last = path+len(1).
      IF del <> last.
        CONCATENATE path del INTO path.
      ENDIF.

*** Document Area ***
     SELECT * FROM sdokdocspt INTO TABLE i_sdokdocspt
               WHERE langu = sy-langu.

* Form read_tables.
      REFRESH i_srep_initial.
      SELECT * FROM crep INTO CORRESPONDING FIELDS OF TABLE i_srep.
      LOOP AT i_srep.
        SELECT SINGLE * FROM crepdescr INTO
                                       CORRESPONDING FIELDS OF i_srep
                                       WHERE crep_id = i_srep-crep_id
                                             AND langu = sy-langu.
        SELECT SINGLE * FROM crepdocsp INTO
                                       CORRESPONDING FIELDS OF i_srep
                                       WHERE crep_id = i_srep-crep_id.
        CASE i_srep-crep_type.
          WHEN scmst_crtyp_ftp.
            SELECT SINGLE * FROM crep_ftp INTO
                                 CORRESPONDING FIELDS OF i_srep
                                 WHERE crep_id = i_srep-crep_id.
          WHEN scmst_crtyp_r3db.
            SELECT SINGLE * FROM crep_r3db INTO
                                 CORRESPONDING FIELDS OF i_srep
                                 WHERE crep_id = i_srep-crep_id.

          WHEN scmst_crtyp_http.
            SELECT SINGLE * FROM crep_http INTO
                                 CORRESPONDING FIELDS OF i_srep
                                 WHERE crep_id = i_srep-crep_id.
          WHEN scmst_crtyp_tree.
            SELECT SINGLE * FROM crep_tree INTO
                                 CORRESPONDING FIELDS OF i_srep
                                 WHERE crep_id = i_srep-crep_id.
          WHEN scmst_crtyp_rfc.
            SELECT SINGLE * FROM crep_rfc INTO
                                 CORRESPONDING FIELDS OF i_srep
                                 WHERE crep_id = i_srep-crep_id.
          WHEN scmst_crtyp_rpc.
            SELECT SINGLE * FROM crep_rpc INTO
                                 CORRESPONDING FIELDS OF i_srep
                                 WHERE crep_id = i_srep-crep_id.
          WHEN scmst_crtyp_file.
            SELECT SINGLE * FROM crep_file INTO
                                 CORRESPONDING FIELDS OF i_srep
                                 WHERE crep_id = i_srep-crep_id.
          WHEN scmst_crtyp_lrep.
            SELECT SINGLE * FROM crep_lrep INTO
                                 CORRESPONDING FIELDS OF i_srep
                                 WHERE crep_id = i_srep-crep_id.
        ENDCASE.

        tst = i_srep-crea_time.
        CONVERT TIME STAMP tst TIME ZONE sy-zonlo INTO
                DATE i_srep-crea_d TIME i_srep-crea_t.
        tst = i_srep-chng_time.
        CONVERT TIME STAMP tst TIME ZONE sy-zonlo INTO
                DATE i_srep-chng_d TIME i_srep-chng_t.

        READ TABLE creptypes_all WITH KEY key = i_srep-crep_type
                   INTO creptype.
        i_srep-text = creptype-text.

        user_name = i_srep-chng_user.
        CALL FUNCTION 'SUSR_USER_ADDRESS_READ'
          EXPORTING
            user_name              = user_name
          IMPORTING
            user_address           = user_address
          EXCEPTIONS
            user_address_not_found = 1
            OTHERS                 = 2.
        IF sy-subrc = 0.
          i_srep-chng_u = user_address-name_text.
        ENDIF.

        user_name = i_srep-crea_user.
        CALL FUNCTION 'SUSR_USER_ADDRESS_READ'
          EXPORTING
            user_name              = user_name
          IMPORTING
            user_address           = user_address
          EXCEPTIONS
            user_address_not_found = 1
            OTHERS                 = 2.
        IF sy-subrc = 0.
          i_srep-crea_u = user_address-name_text.
        ENDIF.

        MODIFY i_srep.
        APPEND i_srep TO i_srep_initial.
      ENDLOOP.

      SORT i_srep BY crep_id.
      PERFORM build_toaar TABLES i_toaar_initial.

* Begin of changes in V002
      IF i_srep[] IS INITIAL.
        ls_output = lc_msg_nodata.
        APPEND ls_output TO lt_output.
        CLEAR ls_output.
        RETURN.
      ENDIF.
* End of changes in V002

*     Column Headings
*V001 - Begin of changes
      CONCATENATE 'Content Repository'         lc_sep
                   'Description'               lc_sep
                   'Document Area'             lc_sep
                   'Document Area Description' lc_sep
                   'Storage Type'              lc_sep
                   'Storage Type Description'  lc_sep
                   'Repository Sub-type'       lc_sep
                   'Sub-type Description'      lc_sep
                   'Version'                   lc_sep
                   'Version Text'              lc_sep
                   'Content Table'             lc_sep
                   'HTTP server'               lc_sep
                   'Port Number'               lc_sep
                   'SSL Port Number'           lc_sep
                   'HTTP Script'               lc_sep
                   'Test Connection'           lc_sep
                   'Physical Path'             lc_sep
                   'Creation Time'             lc_sep
                   'Creation User'             lc_sep
                   'Change Time'               lc_sep
                   'Change User'
*V001 - End of changes
                    INTO ls_output.
      APPEND ls_output TO lt_output.


* Output data
      LOOP AT i_srep.

* Document Area
        READ TABLE i_sdokdocspt WITH KEY docuspace = i_srep-docuspace.
        IF sy-subrc = 0.
          lv_document = i_sdokdocspt-descript.
        ELSE.
          CLEAR lv_document.
        ENDIF.

* Version Text
        READ TABLE crepversions INTO crepversion
          WITH KEY key = i_srep-version.
        IF sy-subrc <> 0.
          CLEAR crepversion.
        ENDIF.

* Rep sub-type
        READ TABLE crepstypes INTO crepstype
          WITH KEY key  = i_srep-crep_stype.
        IF sy-subrc <> 0.
          CLEAR crepstype.
        ENDIF.

        CLEAR lv_test_connection.
        CASE i_srep-crep_type.
          WHEN scmst_crtyp_http.

            CALL FUNCTION 'SCMS_HTTP_PING'
              EXPORTING
                crep_id    = i_srep-crep_id
                security   = 'N'
                http_serv  = i_srep-http_serv
                http_port  = i_srep-http_port
                http_sport = i_srep-http_sport
                http_scrpt = i_srep-http_scrpt
                version    = i_srep-version
              EXCEPTIONS
                error_http = 1
                OTHERS     = 2.
            IF sy-subrc <> 0.
              MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
                         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4
                         INTO lv_test_connection.
            ELSE.
              MESSAGE s053(cms) WITH i_srep-crep_id
              INTO lv_test_connection.
            ENDIF.

          WHEN OTHERS.

        ENDCASE.

* Begin of changes in V002
       IF i_srep-chng_user IS INITIAL.
         i_srep-chng_user = lc_null.
       ENDIF.
* End of changes in V002

        CONCATENATE i_srep-crep_id lc_sep
                    i_srep-descript lc_sep
                    i_srep-docuspace lc_sep
                    lv_document   lc_sep
                    i_srep-crep_type lc_sep
                    i_srep-text   lc_sep
                    i_srep-crep_stype lc_sep
                    crepstype-text lc_sep
                    i_srep-version lc_sep
                    crepversion-text lc_sep
                    i_srep-r3db_tab lc_sep
                    i_srep-http_serv lc_sep
                    i_srep-http_port lc_sep
                    i_srep-http_sport lc_sep
                    i_srep-http_scrpt lc_sep
                    lv_test_connection lc_sep
                    path lc_sep
                    i_srep-crea_time lc_sep
                    i_srep-crea_user lc_sep
                    i_srep-chng_time lc_sep
                    i_srep-chng_user
                    INTO ls_output.
        APPEND ls_output TO lt_output.
        CLEAR : ls_output.

      ENDLOOP.

      PERFORM null_check TABLES  lt_output.

* Begin of changes in V002
    WHEN OTHERS.
      " Incorrect input
      ls_output = lc_input.
      APPEND ls_output TO lt_output.
      CLEAR ls_output.
* End of changes in V002

  ENDCASE.

ENDFORM.

FORM null_check TABLES lt_table TYPE table.

  DO .
    REPLACE ALL OCCURRENCES OF SUBSTRING '||'
    IN TABLE lt_table WITH '|NULL|'.
    FIND '||' IN TABLE lt_table.
    IF sy-subrc <> 0.
      EXIT .
    ENDIF.
  ENDDO.

ENDFORM.

FORM build_toaar TABLES i_toaar TYPE t_toaar.

  REFRESH i_toaar.
  LOOP AT i_srep.
    CLEAR i_toaar.
    CHECK i_srep-docuspace = 'ARCHLINK'
       OR i_srep-docuspace = space.
    CASE i_srep-crep_type.
      WHEN scmst_crtyp_http.
        i_toaar-http       = 'X'.
        i_toaar-http_serv  = i_srep-http_serv.
        i_toaar-script     = i_srep-http_scrpt.
      WHEN scmst_crtyp_r3db.
        i_toaar-http       = 'X'.
      WHEN scmst_crtyp_rfc.
        i_toaar-rpc_servic = i_srep-rfc_dest.
        i_toaar-rfc        = 'X'.
      WHEN scmst_crtyp_rpc.
        i_toaar-rpc_host   = i_srep-rpc_host.
        i_toaar-rpc_servic = i_srep-rpc_servic.
        i_toaar-rpc        = 'X'.
      WHEN scmst_crtyp_file.
        i_toaar-files      = 'X'.
        i_toaar-filedirect = i_srep-file_path.
      WHEN OTHERS.
        CONTINUE.
    ENDCASE.

    i_toaar-language   = sy-langu.
    i_toaar-objecttext = i_srep-descript.
    i_toaar-archiv_id  = i_srep-crep_id.
    i_toaar-interface  = 'archlink'.
    i_toaar-archivpath = i_srep-archivpath.
    i_toaar-basicpath  = i_srep-basicpath.
    i_toaar-protokoll  = i_srep-fe_prot.
    i_toaar-version    = i_srep-version.
    i_toaar-crea_user  = i_srep-crea_user.
    i_toaar-crea_time  = i_srep-crea_time.
    i_toaar-chng_user  = i_srep-chng_user.
    i_toaar-chng_time  = i_srep-chng_time.
    CHECK i_toaar-archiv_id  = i_srep-crep_id.
    APPEND i_toaar.
  ENDLOOP.

ENDFORM.