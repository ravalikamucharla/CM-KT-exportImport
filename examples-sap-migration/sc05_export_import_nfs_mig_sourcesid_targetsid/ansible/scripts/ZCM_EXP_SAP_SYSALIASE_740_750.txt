*---------------------------------------------------------------------*
* Program Name        :   ZCM_EXP_SAP_SYSALIASE_740_750               *
* Title               :   Capture SAP System Aliases                  *
* Purpose             :   To capture SAP system aliases settings      *
*---------------------------------------------------------------------*
* Related Transaction OR Standard Program References                  *
* /UI2/GW_SYS_ALIAS                                                   *
*---------------------------------------------------------------------*
* Tables              :   SELECT    UPDATE      INSERT      DELETE    *
* /IWFND/C_DFSYAL           X                                         *
* /IWFND/C_DFSYALT          X                                         *
*---------------------------------------------------------------------*
* Program Change History                                              *
*---------------------------------------------------------------------*
*Date        Version   Author       TR#/Local     Description         *
*---------------------------------------------------------------------*
*07-21-2021  Initial   Soumya Ray   Local      To capture SAP system  *
*                                              aliases settings       *
*06-04-2022  V001      Soumya Ray   Local      To pass correct message*
*                                              in case of no data &   *
*                                             lower version adjustment*
*---------------------------------------------------------------------*

REPORT zcm_exp_sap_sysaliase_740_750.

FORM get_config
               TABLES lt_input  STRUCTURE tab512
                      lt_output STRUCTURE tab512.

  CONSTANTS: lc_sep     TYPE c VALUE '|'             LENGTH 1,
             lc_e       TYPE spras VALUE 'E',
             lc_null    TYPE c VALUE 'NULL'          LENGTH 4,
             lc_export  TYPE c VALUE 'EXPORT'        LENGTH 6,
             lc_ninput  TYPE c VALUE
             'No input passed'                       LENGTH 20,
             lc_title   TYPE c VALUE
             'SAP System Alias'                      LENGTH 20,
             lc_sysals  TYPE c VALUE
             'SAP System Alias'                      LENGTH 16,
             lc_descp   TYPE c VALUE 'Description'   LENGTH 11,
             lc_localgw TYPE c VALUE 'Local GW'      LENGTH 8,
             lc_localap TYPE c VALUE 'For Local App' LENGTH 13,
             lc_usmcro  TYPE c VALUE
             'Use Micro hub'                         LENGTH 13,
             lc_rfcdest TYPE c VALUE
             'RFC Destination'                       LENGTH 15,
             lc_softwvr TYPE c VALUE
             'Software Version'                      LENGTH 16,
             lc_sysid   TYPE c VALUE 'System ID'     LENGTH 9,
             lc_client  TYPE c VALUE 'Client'        LENGTH 6,
             lc_wsprov  TYPE c VALUE
             'WS Provider System'                    LENGTH 18,
*  Begin of changes in V001
             lc_nsysal  TYPE c VALUE
             'No data found'                         LENGTH 40,
             lc_naction TYPE c VALUE
             'Incorrect input'                       LENGTH 30.
*  End of changes in V001

  DATA: ls_output  TYPE tab512,
        ls_input   TYPE tab512,
        lv_action  TYPE string,
        ls_dfsyal  TYPE /iwfnd/c_dfsyal,
        ls_dfsyalt TYPE /iwfnd/c_dfsyalt,
        lt_dfsyal  TYPE STANDARD TABLE OF /iwfnd/c_dfsyal,
        lt_dfsyalt TYPE STANDARD TABLE OF /iwfnd/c_dfsyalt.

  CLEAR : ls_output, ls_dfsyal, ls_dfsyalt, ls_input, lv_action.
  FREE: lt_dfsyal, lt_dfsyalt.

  ls_output = lc_title. "Title of the script
  APPEND ls_output TO lt_output.
  CLEAR ls_output.

  " Read input passed
  READ TABLE lt_input INTO ls_input INDEX 1.
  IF sy-subrc NE 0.
    ls_output = lc_ninput. " No input passed
    APPEND ls_output TO lt_output.
    CLEAR ls_output.
    RETURN.
  ENDIF.

  lv_action = ls_input.
  CASE lv_action.
    WHEN lc_export. "EXPORT
      "Fetch system alias data for destination folder
      SELECT * FROM /iwfnd/c_dfsyal
      INTO TABLE lt_dfsyal.
      IF sy-subrc NE 0.
        "No data found
        ls_output = lc_nsysal.
        APPEND ls_output TO lt_output.
        CLEAR ls_output.
        RETURN.
      ENDIF.
      SORT lt_dfsyal BY system_alias ASCENDING.
      "Fetch system alias data description for destination folder
      SELECT * FROM /iwfnd/c_dfsyalt
      INTO TABLE lt_dfsyalt
      FOR ALL ENTRIES IN lt_dfsyal
      WHERE system_alias = lt_dfsyal-system_alias
      AND langu = lc_e.
      IF sy-subrc NE 0.
        FREE: lt_dfsyalt.
      ENDIF.
      SORT lt_dfsyalt BY system_alias ASCENDING.
      "Coloumn heading
      CONCATENATE lc_sysals lc_descp lc_localgw lc_localap lc_usmcro
      lc_rfcdest lc_softwvr lc_sysid lc_client lc_wsprov INTO ls_output
      SEPARATED BY lc_sep.
      APPEND ls_output TO lt_output.
      CLEAR ls_output.

      LOOP AT lt_dfsyal INTO ls_dfsyal.
        READ TABLE lt_dfsyalt INTO ls_dfsyalt
        WITH KEY system_alias = ls_dfsyal-system_alias
        BINARY SEARCH.
        IF sy-subrc NE 0.
          CLEAR ls_dfsyalt.
        ENDIF.

        IF ls_dfsyal-ws_provider_syst IS INITIAL.
          ls_dfsyal-ws_provider_syst = lc_null.
        ENDIF.
        "Data
        "use_micro_hub field is not present in lower version so passed
        "NULL to make it similar for POWER BI
        CONCATENATE ls_dfsyal-system_alias ls_dfsyalt-description
        ls_dfsyal-is_local_iwf ls_dfsyal-is_for_bep
        lc_null "+V001
        ls_dfsyal-rfc_dest
        ls_dfsyal-software_version ls_dfsyal-target_sysid
        ls_dfsyal-target_client ls_dfsyal-ws_provider_syst
        INTO ls_output SEPARATED BY lc_sep.

        APPEND ls_output TO lt_output.
        CLEAR: ls_output, ls_dfsyal, ls_dfsyalt.
      ENDLOOP.

    WHEN OTHERS.
      ls_output = lc_naction. "Action not supported
      APPEND ls_output TO lt_output.
      CLEAR ls_output.
  ENDCASE.
  "Null check in middle fields
  DO .
    REPLACE ALL OCCURRENCES OF SUBSTRING '||'
    IN TABLE lt_output WITH '|NULL|'.
    FIND '||' IN TABLE lt_output.
    IF sy-subrc <> 0.
      EXIT .
    ENDIF.
  ENDDO.

ENDFORM.