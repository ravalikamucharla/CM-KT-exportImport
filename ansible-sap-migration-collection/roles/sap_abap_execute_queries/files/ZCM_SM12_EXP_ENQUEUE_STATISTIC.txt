*---------------------------------------------------------------------*
* Program Name        :   ZCM_SM12_EXP_ENQUEUE_STATISTIC              *
* Title               :   Capture Enqueue Statistics                  *
* Purpose             :   This report is used to capture Enqueue      *
*                         Statistics from SM12 during Cloud Migration *
*---------------------------------------------------------------------*
* Related Transaction OR Standard Program References                  *
* SM12                                                                *
*---------------------------------------------------------------------*
* Tables              :   SELECT    UPDATE      INSERT      DELETE    *
* XXXXX                     X                                         *
*---------------------------------------------------------------------*
* Program Change History                                              *
*---------------------------------------------------------------------*
*Date        Version   Author       TR#/Local     Description         *
*---------------------------------------------------------------------*
*01-03-2021  Initial  Ruchir Kamble  Local        Capture Enqueue     *
*                                                 Statistics in SM12  *
*13-09-2022  V001    Yasaswini       Local        To remove dead code *
*---------------------------------------------------------------------*

REPORT zcm_sm12_exp_enqueue_statistic.

LOAD-OF-PROGRAM.

FORM get_config
               TABLES lt_input  STRUCTURE tab512
                      lt_output STRUCTURE tab512.


  CONSTANTS:
    lc_output    TYPE c VALUE 'Output'              LENGTH 6,
    lc_export    TYPE c VALUE 'EXPORT'              LENGTH 6,
    lc_title     TYPE c VALUE 'Enqueue Statistics'  LENGTH 25,
    lc_inc_input TYPE c VALUE 'Incorrect input'     LENGTH 20,
    lc_noinput   TYPE c VALUE 'No input given'      LENGTH 20,
    lc_err_msg   TYPE c VALUE 'No data found'       LENGTH 20.

  DATA: ls_output    TYPE tab512,
        lv_action    TYPE char6,
        lt_stattable TYPE STANDARD TABLE OF spflist,
* Begin of changes in V001
        ls_stattable TYPE spflist,
        ls_input     TYPE tab512.
* End of changes in V001

  " Clearing variables before use
  CLEAR: ls_output, lv_action.

  FREE: lt_output, lt_stattable.

  " Adding title to the output
  ls_output = lc_output.
  APPEND ls_output TO lt_output.
  CLEAR ls_output.

  READ TABLE lt_input INTO ls_input INDEX 1.
  IF sy-subrc EQ 0.
    lv_action = ls_input.
  ELSE.
    " No input given
    ls_output = lc_noinput.
    APPEND ls_output TO lt_output.
    CLEAR ls_output.
    RETURN.
  ENDIF.

  CASE lv_action.

    WHEN lc_export.               " When action is EXPORT

      " FM to read Enqueue Statistics from SM12
      CALL FUNCTION 'ENQUE_GETSTAT'
        TABLES
          stattable = lt_stattable.

      IF lt_stattable IS NOT INITIAL.

        " Adding title to the output
        ls_output = lc_title.
        APPEND ls_output TO lt_output.
        CLEAR ls_output.

        LOOP AT lt_stattable INTO ls_stattable.

          " Checking empty line is not added in the output
          IF ls_stattable IS NOT INITIAL.
            CONDENSE ls_stattable.
            ls_output = ls_stattable.
            APPEND ls_output TO lt_output.
            CLEAR: ls_output, ls_stattable.
          ENDIF.

        ENDLOOP.

      ELSE.
        " No data found
        ls_output = lc_err_msg.
        APPEND ls_output TO lt_output.
        CLEAR ls_output.

      ENDIF.

    WHEN OTHERS.
      " Incorrect input
      ls_output = lc_inc_input.
      APPEND ls_output TO lt_output.
      CLEAR ls_output.

  ENDCASE.

  FREE: lt_stattable.
ENDFORM.